buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
  dependencies {
    classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
  }
}

plugins {
  id 'maven-publish'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'checkstyle'
  apply plugin: 'com.github.ben-manes.versions'

  group 'com.jamf.reporting'
  version '1.1.0'

  sourceCompatibility = JavaVersion.VERSION_17

  java {
    withJavadocJar()
    withSourcesJar()
  }

  checkstyle {
    toolVersion '8.11'
    maxWarnings 0
    maxErrors 0
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.1'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.1'
    implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.5'

    testRuntimeOnly 'org.junit.platform:junit-platform-commons:1.9.0'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.12.16'
    testRuntimeOnly 'org.objenesis:objenesis:3.3'

    testImplementation 'org.spockframework:spock-core:2.2-groovy-4.0'
  }

  test {
    include '**/*Spec*'

    outputs.upToDateWhen {false}
    testLogging {
      events "passed", "skipped", "failed"
    }
    afterSuite {desc, result ->
      if (!desc.parent) {
        println("${result.resultType} " +
            "(${result.testCount} tests, " +
            "${result.successfulTestCount} successes, " +
            "${result.failedTestCount} failures, " +
            "${result.skippedTestCount} skipped)")
      }
    }

    useJUnitPlatform()
  }

  javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

  dependencyUpdates.resolutionStrategy {
    componentSelection {rules ->
      rules.all {ComponentSelection selection ->
        boolean rejected = ['alpha', 'beta', 'rc', 'rc1', 'rc-1', 'cr', 'm', 'preview', 'b', 'ea'].any {qualifier ->
          selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d\w-+]*/
        }
        if (rejected) {
          selection.reject('Release candidate')
        }
      }
    }
  }
}
